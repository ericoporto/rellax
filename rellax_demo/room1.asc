// room script file
int isJumpingCounter;
int coolDown;
int prevx,  prevy;
bool on_air;
int frame_count;

void do_gravity(Character * chara){
  if(isJumpingCounter>0){
    isJumpingCounter=isJumpingCounter-8;
    chara.y=chara.y-8;
    int frame = chara.Frame;
    on_air = true;
    if(frame >= Game.GetFrameCountForLoop(VCHARJUMP,  chara.Loop)-1){
      frame=0;
    } else {
      if(frame_count%3==0){
        frame++;
      }
    }
    chara.LockViewFrame(VCHARJUMP, chara.Loop, frame, eKeepMoving); 

  } else {
    if(GetWalkableAreaAtRoom(chara.x, chara.y+4)!=0){
      chara.y=chara.y+4;
    } else if(GetWalkableAreaAtRoom(chara.x, chara.y+2)!=0){
      chara.y=chara.y+2;
    } else  if(GetWalkableAreaAtRoom(chara.x, chara.y+1)!=0){
      chara.y=chara.y+1;
    } else {
      // hit a platform or ground
        if(coolDown>0){
          coolDown=0;  
        }
      if(on_air){
        chara.UnlockView(eKeepMoving);
        on_air = false;  
      }

    }
  }
}

bool was_moving;
void is_left_right_pressed(Character * chara){
  bool moved=false;
  int loop = chara.Loop;
  int frame = chara.Frame;
  
  if(IsKeyPressed(eKeyD) || IsKeyPressed(eKeyRightArrow) ){
    loop = 2;
    if(GetWalkableAreaAtRoom(chara.x+4, chara.y-2)!=0){
      chara.x=chara.x+4;
      moved=true;
    } else if(GetWalkableAreaAtRoom(chara.x+2, chara.y-2)!=0){
      chara.x=chara.x+2;
      moved=true;
    } else  if(GetWalkableAreaAtRoom(chara.x+1, chara.y-2)!=0){
      chara.x=chara.x+1;
      moved=true;
    }
  } else if(IsKeyPressed(eKeyA) || IsKeyPressed(eKeyLeftArrow)){
    loop = 1;
    if(GetWalkableAreaAtRoom(chara.x-4, chara.y-2)!=0){
      chara.x=chara.x-4;
      moved=true;
    } else if(GetWalkableAreaAtRoom(chara.x-2, chara.y-2)!=0){
      chara.x=chara.x-2;
      moved=true;
    } else  if(GetWalkableAreaAtRoom(chara.x-1, chara.y-2)!=0){
      chara.x=chara.x-1;
      moved=true;
    }
  } 
  
  if(was_moving){
    was_moving = false;
    
    chara.UnlockView();
  }
  
  was_moving = moved;
  
  if(moved){
    if(frame >= Game.GetFrameCountForLoop(VCHARWALK, loop)-1){
      frame=1;
    } else {
      if(frame_count%3==0){
        frame++;
      }
    }
    chara.LockViewFrame(VCHARWALK, loop, frame, eKeepMoving); 
  }
}

function on_key_press(eKeyCode keycode) {
  if(keycode == eKeyW || keycode == eKeyUpArrow){
    if(isJumpingCounter<=0 && coolDown <=0){
      isJumpingCounter = 56;
      coolDown=40;
    }
  }
}

void repeatedly_execute_always() {
  frame_count++;
  
  if(coolDown>0){
    coolDown=coolDown-1;  
  }
  for(int i=0 ; i<Game.CharacterCount ; i++){
    if(player.Room == character[i].Room){
      Character * pcha = character[i];
      do_gravity(pcha);
    }
  }
  is_left_right_pressed(player);
}

function region1_Standing()
{
  player.ChangeRoom(1, 282, 250);
}
